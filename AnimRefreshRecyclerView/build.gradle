apply plugin: 'com.android.library'
/*提交到bintray的相关配置----start----*/
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'
// 这个version是区分library版本的，因此当我们需要更新library时记得修改这个version
version = "1.0.1"
/*提交到bintray的相关配置----pause----*/

android {
    compileSdkVersion 22
    buildToolsVersion "20.0.0"

    /*提交到bintray的相关配置----resume----*/
    resourcePrefix "AnimRefreshRecyclerView" // 可以随便填，不知道干嘛用的
    /*提交到bintray的相关配置----pause----*/

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:22.2.1'
    /* RecyclerView */
    compile 'com.android.support:recyclerview-v7:22.2.0'
}

/*提交到bintray的相关配置----resume----*/
def siteUrl = 'https://github.com/shichaohui/AnimRefreshRecyclerViewDemo'      // 项目的主页
def gitUrl = 'https://github.com/shichaohui/AnimRefreshRecyclerViewDemo.git'   // Git仓库的url
group = "com.sch.rfview" // Maven Group ID for the artifact，一般填你唯一的包名
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}
artifacts {
    archives sourcesJar
}
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = "maven"  //发布到Bintray的那个仓库里，默认账户有四个库，我们这里上传到maven库
        name = "AnimRefreshRecyclerView"  //发布到Bintray上的项目名字
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}
/*提交到bintray的相关配置----end----*/